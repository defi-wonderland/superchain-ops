# Create a new template or task
# e.g. just new [template|task]
new COMMAND="" TASK_TYPE="":
    #!/usr/bin/env bash
    set -euo pipefail
    
    echo -e "\033[42m                          \033[0m"
    echo -e "\033[42m    superchain-ops cli    \033[0m"
    echo -e "\033[42m                          \033[0m"

    show_usage() {
        echo
        echo "Usage: just new [template|task]"
        echo "Available commands:"
        echo "  • template  - Create a new template"
        echo "  • task      - Create a new task"
        echo
    }

    show_task_types() {
        echo
        echo "Available task types:"
        echo "  • l2taskbase - 'L2TaskBase' task type, these tasks require a superchain address registry and can iterate over multiple L2 chains"
        echo "    eg: https://github.com/ethereum-optimism/superchain-ops/blob/main/test/tasks/mock/template/GasConfigTemplate.sol"
        echo "  • simplebase - 'SimpleBase' task type, these tasks require simple key value registry and are independent of the L2 chains"
        echo "    eg: https://github.com/ethereum-optimism/superchain-ops/blob/main/src/improvements/template/FinanceTemplate.sol"
        echo "  • opcmbasetask - 'OPCMBaseTask' task type, these tasks are used to make delegate calls to the Optimism Contracts Manager and are"
        echo "    dependent on the L2 chains and inherit from L2TaskBase"
        echo "    e.g.: https://github.com/ethereum-optimism/superchain-ops/blob/main/src/improvements/template/OPCMUpgradeV200.sol"
        echo
    }
    
    # shellcheck disable=SC2050
    case "{{COMMAND}}" in
        "")
            echo -e "\n\033[31mError: No command specified\033[0m"
            show_usage
            exit 1
            ;;
        template)
            if [ -z "{{TASK_TYPE}}" ]; then
                echo -e "\n\033[31mError: No task type specified\033[0m"
                show_task_types
                exit 1
            fi

            case "{{TASK_TYPE}}" in
                l2taskbase)
                    TASK_TYPE="L2TaskBase"
                    ;;
                simplebase)
                    TASK_TYPE="SimpleBase"
                    ;;
                opcmbasetask)
                    TASK_TYPE="OPCMBaseTask"
                    ;;
                *)
                    echo -e "\n\033[31mError: Invalid task type '{{TASK_TYPE}}'\033[0m"
                    show_task_types
                    exit 1
                    ;;
            esac
            ./script/create-template.sh "$TASK_TYPE"
            ;;
        task)
            if [ -n "{{TASK_TYPE}}" ]; then
                echo -e "\n\033[31mError: Task type should not be specified for 'task' command\033[0m"
                exit 1
            fi
            ./script/create-task.sh
            ;;
        *)
            echo -e "\n\033[31mError: Invalid command '{{COMMAND}}'\033[0m"
            show_usage
            exit 1
            ;;
    esac

task COMMAND="" NETWORK="":
    #!/usr/bin/env bash
    set -euo pipefail

    echo -e "\033[42m                          \033[0m"
    echo -e "\033[42m    superchain-ops cli    \033[0m"
    echo -e "\033[42m                          \033[0m"

    if [ "{{COMMAND}}" = "" ]; then
        echo -e "\n\033[31mError: No command specified\033[0m\n"
        exit 1
    fi

    # shellcheck disable=SC2194
    case "{{COMMAND}}" in
        ls)
            echo ""
            ./script/sorted-tasks.sh {{NETWORK}}
            ;;
        *)
            echo -e "\n\033[31mError: Invalid command '{{COMMAND}}'\033[0m\n"
            exit 1
            ;;
    esac
    
monorepo-integration-test COMMAND="":
    #!/usr/bin/env bash
    set -euo pipefail

    # Set FOUNDRY_PROFILE, defaulting to 'default' if not already set.
    export FOUNDRY_PROFILE="${FOUNDRY_PROFILE:-default}"
    echo "Currently running with FOUNDRY_PROFILE: ${FOUNDRY_PROFILE}"

    root_dir=$(git rev-parse --show-toplevel)
    allocs_path="${root_dir}/lib/optimism/packages/contracts-bedrock/allocs.json"

    # Running this command with mainnet RPC URL.
    ETH_RPC_URL=$(yq eval ".profile.\"${FOUNDRY_PROFILE}\".rpc_endpoints.mainnet" "${root_dir}/foundry.toml")
    export ETH_RPC_URL
    echo "Using mainnet RPC: ${ETH_RPC_URL}"
    
    # For now, we are running monorepo integration tests for the example eth tasks only.
    mainnet_network_task_dir="eth" 

    forge build
    forge script ${root_dir}/src/improvements/tasks/TaskRunner.sol:TaskRunner --sig "run(string,string)" ${allocs_path} ${mainnet_network_task_dir} --ffi --rpc-url $ETH_RPC_URL
    export SUPERCHAIN_OPS_ALLOCS_PATH=./allocs.json
    cd ${root_dir}/lib/optimism/packages/contracts-bedrock/

    export NO_MATCH_CONTRACTS="ETHLockboxTest\|L1CrossDomainMessenger_ReinitReentryTest\|L1CrossDomainMessenger_Test\|L1ERC721Bridge_Pause_Test\|L1ERC721Bridge_Pause_TestFail\|L1ERC721Bridge_Test\|L1StandardBridge_BridgeETHTo_Test\|L1StandardBridge_BridgeETH_Test\|L1StandardBridge_DepositERC20To_Test\|L1StandardBridge_DepositERC20_Test\|L1StandardBridge_DepositERC20_TestFail\|L1StandardBridge_DepositETHTo_Test\|L1StandardBridge_DepositETH_Test\|L1StandardBridge_DepositETH_TestFail\|L1StandardBridge_FinalizeBridgeETH_Test\|L1StandardBridge_FinalizeBridgeETH_TestFail\|L1StandardBridge_FinalizeERC20Withdrawal_Test\|L1StandardBridge_FinalizeERC20Withdrawal_TestFail\|L1StandardBridge_FinalizeETHWithdrawal_Test\|L1StandardBridge_Getter_Test\|L1StandardBridge_Initialize_Test\|L1StandardBridge_Pause_Test\|L1StandardBridge_Pause_TestFail\|L1StandardBridge_Receive_Test\|OptimismPortal2_FinalizeWithdrawal_Test\|OptimismPortal2_LiquidityMigration_Test\|OptimismPortal2_ResourceFuzz_Test\|OptimismPortal2_Test\|OptimismPortal2_Upgradeable_Test\|OptimismPortal2_upgrade_Test\|ProtocolVersions_Initialize_Test\|ProtocolVersions_Setters_Test\|ProtocolVersions_Setters_TestFail\|StandardValidatorV180_Test\|SuperchainConfig_Init_Test\|SuperchainConfig_Pause_Test\|SuperchainConfig_Pause_TestFail\|SuperchainConfig_Unpause_Test\|SuperchainConfig_Unpause_TestFail\|SystemConfig_Init_ResourceConfig\|SystemConfig_Initialize_Test\|SystemConfig_Initialize_TestFail\|SystemConfig_Setters_Test\|SystemConfig_Setters_TestFail\|SystemConfig_upgrade_Test\|MIPS_Test\|MIPS2_Test\|MIPS64Memory_Test\|AnchorStateRegistry_Anchors_Test\|AnchorStateRegistry_GetAnchorRoot_Test\|AnchorStateRegistry_Initialize_Test\|AnchorStateRegistry_Initialize_TestFail\|AnchorStateRegistry_IsGameBlacklisted_Test\|AnchorStateRegistry_IsGameClaimValid_Test\|AnchorStateRegistry_IsGameFinalized_Test\|AnchorStateRegistry_IsGameProper_Test\|AnchorStateRegistry_IsGameRegistered_Test\|AnchorStateRegistry_IsGameResolved_Test\|AnchorStateRegistry_IsGameRespected_Test\|AnchorStateRegistry_IsGameRetired_Test\|AnchorStateRegistry_Paused_Test\|AnchorStateRegistry_SetAnchorState_Test\|AnchorStateRegistry_SetAnchorState_TestFail\|AnchorStateRegistry_Version_Test\|AnchorStateRegistry_blacklistDisputeGame_Test\|AnchorStateRegistry_blacklistDisputeGame_TestFail\|AnchorStateRegistry_setRespectedGameType_Test\|AnchorStateRegistry_setRespectedGameType_TestFail\|AnchorStateRegistry_updateRetirementTimestamp_Test\|AnchorStateRegistry_updateRetirementTimestamp_TestFail\|DelayedWETH_Hold_Test\|DelayedWETH_Initialize_Test\|DelayedWETH_Recover_Test\|DelayedWETH_Unlock_Test\|DelayedWETH_WithdrawFrom_Test\|DelayedWETH_Withdraw_Test\|DisputeGameFactory_Create_Test\|DisputeGameFactory_FindLatestGames_Test\|DisputeGameFactory_GetGameUUID_Test\|DisputeGameFactory_Owner_Test\|DisputeGameFactory_SetImplementation_Test\|DisputeGameFactory_SetInitBond_Test\|DisputeGameFactory_TransferOwnership_Test\|FaultDisputeGame_Test\|FaultDispute_1v1_Actors_Test\|PermissionedDisputeGame_Test\|SuperFaultDisputeGame_Test\|SuperFaultDispute_1v1_Actors_Test\|SuperPermissionedDisputeGame_Test"
    # shellcheck disable=SC2194
    case "{{COMMAND}}" in
        rerun)
            just test-upgrade-rerun --no-match-contract "${NO_MATCH_CONTRACTS}"
            ;;
        *)
            just test-upgrade --no-match-contract "${NO_MATCH_CONTRACTS}"
            ;;
    esac
    rm -f ${allocs_path} # clean up

check-superchain-registry-latest:
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)
    bash ${root_dir}/src/improvements/script/check-superchain-latest.sh

# this is command is mainly for CI testing purposes and not task developers
# task developers can run this locally, however it is expected that this
# command will be run mostly by CI.
simulate-all-templates:
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)
    forge build

    # If the task is nested then we only simulate as the foundation.
    # In the future we could simulate as other nested safes. 
    # For testing purposes, we do not gain anything by simulating as other nested safes.
    nested_safe_name="foundation"
    simulation_count=0
    for task in ${root_dir}/test/tasks/example/*/*; do
        if [ -d "$task" ]; then
            ${root_dir}/src/improvements/script/simulate-task.sh $task $nested_safe_name
            simulation_count=$((simulation_count + 1))
        fi
    done
    echo "$simulation_count simulations run."
    template_count=$(find "${root_dir}/src/improvements/template" -type f ! -name "*.template.sol" | wc -l)

    if [ "$simulation_count" -ne "$template_count" ]; then
        echo "Not all templates files have a corresponding task. Please add a task for a template in ${root_dir}/src/improvements/tasks/example" >&2
        exit 1
    fi

simulate-verify-task TASK="":
    #!/usr/bin/env bash
    set -euo pipefail
    root_dir=$(git rev-parse --show-toplevel)

    forge build

    # If the task is nested then we only simulate as the foundation.
    # In the future we could simulate as other nested safes. 
    # For testing purposes, we do not gain anything by simulating as other nested safes.
    nested_safe_name="foundation"

    ${root_dir}/src/improvements/script/simulate-verify-task.sh {{TASK}} $nested_safe_name
